package options;

import flixel.FlxG;
import flixel.FlxSprite;
import flixel.group.FlxGroup.FlxTypedGroup;
import flixel.text.FlxText.FlxTextBorderStyle;
import flixel.util.FlxColor;
import shaders.ColorSwap;
import objects.Note;

using StringTools;

class NotesColorLegacySubState extends MusicBeatSubstate
{
	private static var curSelected:Int = 0;
	private static var typeSelected:Int = 0;

	private var grpNumbers:FlxTypedGroup<Alphabet>;
	private var grpNotes:FlxTypedGroup<FlxSprite>;
	private var shaderArray:Array<ColorSwap> = [];
	var curValue:Float = 0;
	var holdTime:Float = 0;
	var nextAccept:Int = 5;

	var blackBG:FlxSprite;
	var hsbTexts:FlxTypedGroup<FlxText>;

	static inline var NOTE_X:Float = 230;
	static inline var VALUE_START_X:Float = 480;
	static inline var VALUE_SPACING_X:Float = 225;
	static inline var ROW_START_Y:Float = 35;
	static inline var ROW_SPACING_Y:Float = 165;

	public function new()
	{
		super();

		var bg:FlxSprite = new FlxSprite().loadGraphic(Paths.image('menuDesat'));
		bg.color = 0xFFea71fd;
		bg.screenCenter();
		bg.antialiasing = ClientPrefs.data.antialiasing;
		add(bg);

		blackBG = new FlxSprite(NOTE_X - 25).makeGraphic(900, 200, FlxColor.BLACK);
		blackBG.alpha = 0.4;
		add(blackBG);

		grpNotes = new FlxTypedGroup<FlxSprite>();
		add(grpNotes);
		grpNumbers = new FlxTypedGroup<Alphabet>();
		add(grpNumbers);

		for (i in 0...ClientPrefs.data.arrowHSV.length)
		{
			var yPos:Float = (ROW_SPACING_Y * i) + ROW_START_Y;
			for (j in 0...3)
			{
				var optionText:Alphabet = new Alphabet(VALUE_START_X + (VALUE_SPACING_X * j), yPos + 60, Std.string(Std.int(ClientPrefs.data.arrowHSV[i][j])),
					true);
				optionText.alignment = CENTERED;
				grpNumbers.add(optionText);
			}

			var note:FlxSprite = new FlxSprite(NOTE_X, yPos);
			note.frames = Paths.getSparrowAtlas(Note.resolveNoteSkinPath(null, false));
			var animations:Array<String> = ['purple0', 'blue0', 'green0', 'red0'];
			note.animation.addByPrefix('idle', animations[i % animations.length]);
			note.animation.play('idle');
			note.antialiasing = ClientPrefs.data.antialiasing;
			grpNotes.add(note);

			var newShader:ColorSwap = new ColorSwap();
			note.shader = newShader.shader;
			newShader.hue = ClientPrefs.data.arrowHSV[i][0] / 360;
			newShader.saturation = ClientPrefs.data.arrowHSV[i][1] / 100;
			newShader.brightness = ClientPrefs.data.arrowHSV[i][2] / 100;
			shaderArray.push(newShader);
		}

		hsbTexts = new FlxTypedGroup<FlxText>();
		add(hsbTexts);
		var labels:Array<String> = ['Hue', 'Saturation', 'Brightness'];
		for (i in 0...labels.length)
		{
			var label:FlxText = new FlxText(VALUE_START_X + (VALUE_SPACING_X * i) - 45, 0, 180, labels[i], 24);
			label.setFormat(Paths.font('vcr.ttf'), 24, FlxColor.WHITE, CENTER, FlxTextBorderStyle.OUTLINE, FlxColor.BLACK);
			label.borderSize = 2;
			hsbTexts.add(label);
		}

		changeSelection();
	}

	var changingNote:Bool = false;

	override function update(elapsed:Float)
	{
		if (changingNote)
		{
			if (holdTime < 0.5)
			{
				if (controls.UI_LEFT_P)
				{
					updateValue(-1);
					FlxG.sound.play(Paths.sound('scrollMenu'));
				}
				else if (controls.UI_RIGHT_P)
				{
					updateValue(1);
					FlxG.sound.play(Paths.sound('scrollMenu'));
				}
				else if (controls.RESET)
				{
					resetValue(curSelected, typeSelected);
					FlxG.sound.play(Paths.sound('scrollMenu'));
				}
				if (controls.UI_LEFT_R || controls.UI_RIGHT_R)
					holdTime = 0;
				else if (controls.UI_LEFT || controls.UI_RIGHT)
					holdTime += elapsed;
			}
			else
			{
				var add:Float = 90;
				switch (typeSelected)
				{
					case 1 | 2:
						add = 50;
				}
				if (controls.UI_LEFT)
					updateValue(elapsed * -add);
				else if (controls.UI_RIGHT)
					updateValue(elapsed * add);
				if (controls.UI_LEFT_R || controls.UI_RIGHT_R)
				{
					FlxG.sound.play(Paths.sound('scrollMenu'));
					holdTime = 0;
				}
			}
		}
		else
		{
			if (controls.UI_UP_P)
			{
				changeSelection(-1);
				FlxG.sound.play(Paths.sound('scrollMenu'));
			}
			if (controls.UI_DOWN_P)
			{
				changeSelection(1);
				FlxG.sound.play(Paths.sound('scrollMenu'));
			}
			if (controls.UI_LEFT_P)
			{
				changeType(-1);
				FlxG.sound.play(Paths.sound('scrollMenu'));
			}
			if (controls.UI_RIGHT_P)
			{
				changeType(1);
				FlxG.sound.play(Paths.sound('scrollMenu'));
			}
			if (controls.RESET)
			{
				for (i in 0...3)
					resetValue(curSelected, i);
				FlxG.sound.play(Paths.sound('scrollMenu'));
			}
			if (controls.ACCEPT && nextAccept <= 0)
			{
				FlxG.sound.play(Paths.sound('scrollMenu'));
				changingNote = true;
				holdTime = 0;
				for (i in 0...grpNumbers.length)
				{
					var item = grpNumbers.members[i];
					item.alpha = ((curSelected * 3) + typeSelected == i) ? 1 : 0;
				}
				for (i in 0...grpNotes.length)
				{
					var item = grpNotes.members[i];
					item.alpha = (curSelected == i) ? 1 : 0;
				}
				super.update(elapsed);
				return;
			}
		}

		if (controls.BACK || (changingNote && controls.ACCEPT))
		{
			if (!changingNote)
				close();
			else
				changeSelection();
			changingNote = false;
			FlxG.sound.play(Paths.sound('cancelMenu'));
		}

		if (nextAccept > 0)
			nextAccept -= 1;
		super.update(elapsed);
	}

	function changeSelection(change:Int = 0)
	{
		curSelected += change;
		if (curSelected < 0)
			curSelected = ClientPrefs.data.arrowHSV.length - 1;
		if (curSelected >= ClientPrefs.data.arrowHSV.length)
			curSelected = 0;

		curValue = ClientPrefs.data.arrowHSV[curSelected][typeSelected];
		updateValue();

		for (i in 0...grpNumbers.length)
		{
			var item = grpNumbers.members[i];
			item.alpha = ((curSelected * 3) + typeSelected == i) ? 1 : 0.6;
		}
		for (i in 0...grpNotes.length)
		{
			var item = grpNotes.members[i];
			item.alpha = (curSelected == i) ? 1 : 0.6;
			item.scale.set(curSelected == i ? 1 : 0.75, curSelected == i ? 1 : 0.75);
			if (curSelected == i)
			{
				for (label in hsbTexts)
					label.y = item.y - 62;
				blackBG.y = item.y - 20;
			}
		}
	}

	function changeType(change:Int = 0)
	{
		typeSelected += change;
		if (typeSelected < 0)
			typeSelected = 2;
		if (typeSelected > 2)
			typeSelected = 0;

		curValue = ClientPrefs.data.arrowHSV[curSelected][typeSelected];
		updateValue();

		for (i in 0...grpNumbers.length)
		{
			var item = grpNumbers.members[i];
			item.alpha = ((curSelected * 3) + typeSelected == i) ? 1 : 0.6;
		}
	}

	function resetValue(selected:Int, type:Int)
	{
		curValue = 0;
		ClientPrefs.data.arrowHSV[selected][type] = 0;
		switch (type)
		{
			case 0:
				shaderArray[selected].hue = 0;
			case 1:
				shaderArray[selected].saturation = 0;
			case 2:
				shaderArray[selected].brightness = 0;
		}

		var item = grpNumbers.members[(selected * 3) + type];
		item.text = '0';
		Note.globalRgbShaders = [];
	}

	function updateValue(change:Float = 0)
	{
		curValue += change;
		var roundedValue:Int = Math.round(curValue);
		var max:Float = 180;
		switch (typeSelected)
		{
			case 1 | 2:
				max = 100;
		}

		if (roundedValue < -max)
			curValue = -max;
		else if (roundedValue > max)
			curValue = max;
		roundedValue = Math.round(curValue);
		ClientPrefs.data.arrowHSV[curSelected][typeSelected] = roundedValue;
		Note.globalRgbShaders = [];

		switch (typeSelected)
		{
			case 0:
				shaderArray[curSelected].hue = roundedValue / 360;
			case 1:
				shaderArray[curSelected].saturation = roundedValue / 100;
			case 2:
				shaderArray[curSelected].brightness = roundedValue / 100;
		}

		var item = grpNumbers.members[(curSelected * 3) + typeSelected];
		item.text = Std.string(roundedValue);
	}
}


package backend;

import flixel.util.FlxSave;
import flixel.input.keyboard.FlxKey;
import flixel.input.gamepad.FlxGamepadInputID;
import states.TitleState;

// Add a variable here and it will get automatically saved
@:structInit class SaveVariables
{
	// Mobile and Mobile Controls Releated
	public var extraButtons:String = "NONE"; // mobile extra button option
	public var hitboxPos:Bool = true; // hitbox extra button position option
	public var dynamicColors:Bool = true; // yes cause its cool -Karim
	public var controlsAlpha:Float = FlxG.onMobile ? 0.6 : 0;
	public var showTouchPointer:Bool = true; // show touch pointer indicator (like Android dev option)
	public var showMobileDebugButtons:Bool = false; // show the trace button on mobile
	public var screensaver:Bool = false;
	public var infinityDisplay:Bool = false; // Extend viewport vertically for modern screens while keeping game at 16:9
	#if android
	public var storageType:String = "EXTERNAL_DATA";
	public var androidOptimizationsApplied:Bool = false; // One-time optimization flag
	public var androidOptimizationProfileVersion:Int = 0;
	#end
	public var hitboxType:String = "Gradient";
	public var popUpRating:Bool = true;
	public var versionTextOnGameplay:Bool = false;
	public var vsliceNaughtyness:Bool = true;
	public var vsliceSubtitles:Bool = true;
	public var vsliceStrumlineBackgroundOpacity:Int = 0;
	public var vsliceScreenshotHideMouse:Bool = true;
	public var vsliceScreenshotFancyPreview:Bool = true;
	public var vsliceScreenshotPreviewOnSave:Bool = true;
	public var vsliceAutoFullscreen:Bool = false;
	public var vsliceHapticsMode:String = "All";
	public var vsliceHapticsIntensity:Float = 1;
	public var gameOverVibration:Bool = false;
	public var fpsRework:Bool = false;
	public var framerateMode:String = 'Psych';
	public var uncapFramerate:Bool = false;
	public var mobileReceptorAlign:Bool = false; // Align receptors with hitbox lanes (mobile only, may break scripts)
	#if windows
	public var fullscreenMode:String = 'Borderless'; // 'Borderless', 'Borderless Fix', 'Exclusive'
	public var windowsGDIEffects:Bool = false; // Requires manual user consent in Gameplay Settings
	#end
	public var accuracySystem:String = 'Psych'; // 'Wife3', 'Psych', 'Simple', 'osu!mania', 'DJMAX', 'ITG'
	public var badShitBreakCombo:Bool = false; // When true, Bad and Shit will break the combo
	public var systemScoreMultiplier:String = 'Psych'; // 'Psych', 'Codename'
	public var downScroll:Bool = false;
	public var middleScroll:Bool = false;
	public var opponentStrums:Bool = true;
	public var showFPS:Bool = true;
	public var vsync:Bool = false;
	public var fpsCounterMode:String = #if mobile 'Visible No Background' #else 'Visible with Background' #end; // FPS counter visibility/detail mode
	public var fpsDebugLevel:Int = #if mobile 1 #else 2 #end; // Legacy FPSCounter debug level (persistent)
	public var showWatermark:Bool = false;
	public var flashing:Bool = true;
	public var autoPause:Bool = true;
	public var instantWindowClose:Bool = true;
	public var antialiasing:Bool = true;
	#if windows
	public var changeWindowBorderColorWithNoteHit:Bool = false; // Changes window border color on note hit (Windows 11 only)
	#end
	public var noteSkin:String = 'Default';
	public var noteRGB:Bool = true;
	public var splashSkin:String = 'Psych';
	public var splashAlpha:Float = 0.6;
	public var colorQuantization:Bool = false; // StepMania-style color quantization
	public var menuAccentColor:String = 'Purple';
	public var menuAccentColorCustom:Int = 0xFF6F52D8;
	public var menuDarkTheme:Bool = false;
	public var menuThemeMode:String = 'Light';
	public var lowQuality:Bool = false;
	public var shaders:Bool = true;
	public var colorblindMode:String = 'None';
	public var cacheOnGPU:Bool = #if !switch false #else true #end; // GPU Caching made by Raltyro
	public var framerate:Int = 60;
	public var camZooms:Bool = true;
	public var hideHud:Bool = false;
	public var hideSustainSplash:Bool = true;
	public var showKeyViewer:Bool = false;
	public var iconBounceType:String = 'Default';
	public var judgementCounter:Bool = false;
	public var showRating:Bool = true;
	public var showCombo:Bool = false;
	public var showComboNum:Bool = true;
	public var showEarlyLateSprites:Bool = false;
	public var showHitMs:Bool = false;
	public var comboInGame:Bool = false;
	public var nfRatingStyle:Bool = false;
	public var showEndCountdown:Bool = false; // Enables/disables the end countdown
	public var endCountdownSeconds:Int = 10; // End countdown seconds (10-30)
	public var camera3dEnabled:Bool = true; // Enables 3D camera transformations
	public var zScale:Float = 1.0; // Z-axis depth scale (0.1-5.0)
	public var renderArrowPaths:Bool = false; // Renders arrow trajectory lines (performance intensive)
	public var styledArrowPaths:Bool = false; // Applies colors/transparency to arrow paths
	public var arrowPathBoundary:Int = 300; // Pixels beyond screen to render paths (0-1000)
	public var optimizeHolds:Bool = false; // Optimizes hold rendering (not recommended for complex modcharts)
	public var holdsBehindStrum:Bool = false; // Renders sustains behind strum line
	public var holdEndScale:Float = 1.0; // Scale multiplier for hold note endings (0.1-3.0)
	public var holdCacheEnabled:Bool = true; // Hold graphics cache for performance
	public var holdAlphaDivisions:Int = 20; // Pre-calculated alpha variants (10-30)
	public var columnSpecificModifiers:Bool = true; // Enables per-lane modifier calculations
	public var modchartDebug:Bool = false; // Shows the NotITG-style modchart debug overlay

	public var noteOffset:Int = 0;
	public var arrowRGB:Array<Array<FlxColor>> = [
		[0xFFC24B99, 0xFFFFFFFF, 0xFF3C1F56],
		[0xFF00FFFF, 0xFFFFFFFF, 0xFF1542B7],
		[0xFF12FA05, 0xFFFFFFFF, 0xFF0A4447],
		[0xFFF9393F, 0xFFFFFFFF, 0xFF651038]
	];
	public var arrowRGBPixel:Array<Array<FlxColor>> = [
		[0xFFE276FF, 0xFFFFF9FF, 0xFF60008D],
		[0xFF3DCAFF, 0xFFF4FFFF, 0xFF003060],
		[0xFF71E300, 0xFFF6FFE6, 0xFF003100],
		[0xFFFF884E, 0xFFFFFAF5, 0xFF6C0000]
	];
	public var arrowHSV:Array<Array<Float>> = [[0, 0, 0], [0, 0, 0], [0, 0, 0], [0, 0, 0]];

	public var ghostTapping:Bool = true;
	public var timeBarType:String = 'Time Left';
	public var shadedTimeBar:Bool = false;
	public var scoreZoom:Bool = true;
	public var timeBump:Bool = false;
	public var noReset:Bool = false;
	public var healthBarAlpha:Float = 1;
	public var smoothHealthBar:Bool = true;
	public var usePsychScoreText:Bool = true;
	public var hitsoundVolume:Float = 0;
	public var hitSounds:String = "None";
	public var hitsoundType:String = "None";
	public var pauseMusic:String = 'Tea Time';
	public var checkForUpdates:Bool = true;
	public var comboStacking:Bool = true;
	public var gameplaySettings:Map<String, Dynamic> = [
		'scrollspeed' => 1.0,
		'scrolltype' => 'multiplicative',
		// anyone reading this, amod is multiplicative speed mod, cmod is constant speed mod, and xmod is bpm based speed mod.
		// an amod example would be chartSpeed * multiplier
		// cmod would just be constantSpeed = chartSpeed
		// and xmod basically works by basing the speed on the bpm.
		// iirc (beatsPerSecond * (conductorToNoteDifference / 1000)) * noteSize (110 or something like that depending on it, prolly just use note.height)
		// bps is calculated by bpm / 60
		// oh yeah and you'd have to actually convert the difference to seconds which I already do, because this is based on beats and stuff. but it should work
		// just fine. but I wont implement it because I don't know how you handle sustains and other stuff like that.
		// oh yeah when you calculate the bps divide it by the songSpeed or rate because it wont scroll correctly when speeds exist.
		// -kade
		'songspeed' => 1.0,
		'healthgain' => 1.0,
		'healthloss' => 1.0,
		'instakill' => false,
		'practice' => false,
		'botplay' => false,
		'opponentdrain' => false, // JS Engine-style: opponent note hits drain player health
		'opponentplay' => false,
		'perfect' => false, // Perfect Mode - insta-kill on any judgement below Sick
		'nodroppenalty' => false // Hold drops don't cause misses
	];

	public var comboOffset:Array<Int> = [0, 0, 0, 0];
	public var hitMsOffset:Array<Int> = [0, 0];
	public var keyViewerOffset:Array<Int> = [0, 0]; // X, Y offset for key viewer
	public var keyViewerColor:String = 'Gray'; // Color name for key viewer
	public var ratingOffset:Int = 0;
	public var useFlawlessRating:Bool = false;
	public var flawlessWindow:Float = 20.0;
	public var sickWindow:Float = 45.0;
	public var goodWindow:Float = 90.0;
	public var badWindow:Float = 135.0;
	public var safeFrames:Float = 10.0;
	public var guitarHeroSustains:Bool = false;
	public var discordRPC:Bool = true;
	public var language:String = 'en-US';
	public var abbreviateScore:Bool = true;
	public var dynamicComboDigits:Bool = false;
	public var newfreeplay:Bool = true;
	public var resultsStateAtEnd:Bool = true;
	public var vanillaTransition:Bool = false; // Use vanilla Psych Engine transition instead of custom
	public var pauseCountdown:Bool = false; // Enable countdown when resuming from pause
	public var heyIntro:Bool = false; // Boyfriend and Girlfriend do Hey! animation on countdown Go!
	public var breakTimer:Bool = false; // Show timer when next notes are approaching
	public var usePsychFreeplay:Bool = true; // Use Psych-style legacy Freeplay instead of PlusEngine Freeplay
	public var scriptDeprecationWarnings:Bool = true; // Show warnings for deprecated Lua/HScript compatibility APIs
	public var modSecurityEnabled:Bool = true; // Scan mod Lua/HScript before allowing sensitive APIs to run
	public var modSecurityChecks:Map<String, Bool> = new Map();
	public var dragCharacterToMove:Bool = false; // Allow to drag position character with cursor like in Codename Engine
}

class ClientPrefs
{
	public static var data:SaveVariables = {};
	public static var defaultData:SaveVariables = {};
	public static var globalAntialiasing(get, set):Bool;
	public static var judgementCounter:Bool = false;
	public static inline var FRAMERATE_MAX:Int = 240;
	public static inline var FRAMERATE_UNCAPPED:Int = 1000;
	public static final FRAMERATE_MODES:Array<String> = ['Psych', 'Fixed', 'Interpolated'];

	// Every key has two binds, add your key bind down here and then add your control on options/ControlsSubState.hx and Controls.hx
	public static var keyBinds:Map<String, Array<FlxKey>> = [
		// Key Bind, Name for ControlsSubState
		'note_up' => [W, UP],
		'note_left' => [A, LEFT],
		'note_down' => [S, DOWN],
		'note_right' => [D, RIGHT],
		'ui_up' => [W, UP],
		'ui_left' => [A, LEFT],
		'ui_down' => [S, DOWN],
		'ui_right' => [D, RIGHT],
		'accept' => [SPACE, ENTER],
		'back' => [BACKSPACE, ESCAPE],
		'pause' => [ENTER, ESCAPE],
		'reset' => [R],
		'volume_mute' => [ZERO],
		'volume_up' => [NUMPADPLUS, PLUS],
		'volume_down' => [NUMPADMINUS, MINUS],
		'debug_1' => [SEVEN],
		'debug_2' => [EIGHT],
		'debug_3' => [SIX],
		'fullscreen' => [F11]
	];
	public static var gamepadBinds:Map<String, Array<FlxGamepadInputID>> = [
		'note_up' => [DPAD_UP, Y],
		'note_left' => [DPAD_LEFT, X],
		'note_down' => [DPAD_DOWN, A],
		'note_right' => [DPAD_RIGHT, B],
		'ui_up' => [DPAD_UP, LEFT_STICK_DIGITAL_UP],
		'ui_left' => [DPAD_LEFT, LEFT_STICK_DIGITAL_LEFT],
		'ui_down' => [DPAD_DOWN, LEFT_STICK_DIGITAL_DOWN],
		'ui_right' => [DPAD_RIGHT, LEFT_STICK_DIGITAL_RIGHT],
		'accept' => [A, START],
		'back' => [B],
		'pause' => [START],
		'reset' => [BACK]
	];
	public static var mobileBinds:Map<String, Array<MobileInputID>> = [
		'note_up' => [NOTE_UP],
		'note_left' => [NOTE_LEFT],
		'note_down' => [NOTE_DOWN],
		'note_right' => [NOTE_RIGHT],
		'ui_up' => [UP],
		'ui_left' => [LEFT],
		'ui_down' => [DOWN],
		'ui_right' => [RIGHT],
		'accept' => [A],
		'back' => [B],
		'pause' => [#if android NONE #else P #end],
		'reset' => [NONE]
	];
	public static var defaultKeys:Map<String, Array<FlxKey>> = null;
	public static var defaultButtons:Map<String, Array<FlxGamepadInputID>> = null;
	public static var defaultMobileBinds:Map<String, Array<MobileInputID>> = null;
	static var controlsSaveCache:FlxSave = null;

	static function get_globalAntialiasing():Bool
	{
		return data.antialiasing;
	}

	static function set_globalAntialiasing(value:Bool):Bool
	{
		data.antialiasing = value;
		return value;
	}

	static function getControlsSave():FlxSave
	{
		if (controlsSaveCache == null)
		{
			controlsSaveCache = new FlxSave();
			controlsSaveCache.bind('controls_v3', CoolUtil.getSavePath());
		}
		return controlsSaveCache;
	}

	public static function resetKeys(controller:Null<Bool> = null) // Null = both, False = Keyboard, True = Controller
	{
		if (controller != true)
			for (key in keyBinds.keys())
				if (defaultKeys.exists(key))
					keyBinds.set(key, defaultKeys.get(key).copy());

		if (controller != false)
			for (button in gamepadBinds.keys())
				if (defaultButtons.exists(button))
					gamepadBinds.set(button, defaultButtons.get(button).copy());
	}

	public static function clearInvalidKeys(key:String)
	{
		var keyBind:Array<FlxKey> = keyBinds.get(key);
		var gamepadBind:Array<FlxGamepadInputID> = gamepadBinds.get(key);
		var mobileBind:Array<MobileInputID> = mobileBinds.get(key);
		while (keyBind != null && keyBind.contains(NONE))
			keyBind.remove(NONE);
		while (gamepadBind != null && gamepadBind.contains(NONE))
			gamepadBind.remove(NONE);
		while (mobileBind != null && mobileBind.contains(NONE))
			mobileBind.remove(NONE);
	}

	public static function loadDefaultKeys()
	{
		defaultKeys = keyBinds.copy();
		defaultButtons = gamepadBinds.copy();
		defaultMobileBinds = mobileBinds.copy();
	}

	#if android
	public static function loadStorageTypeEarly():Void
	{
		var save:FlxSave = new FlxSave();
		save.bind('funkin', CoolUtil.getSavePath());
		if (save != null && save.data != null && Reflect.hasField(save.data, 'storageType'))
		{
			var storedType = Reflect.field(save.data, 'storageType');
			if (storedType != null)
			{
				data.storageType = switch (storedType)
				{
					case 'EXTERNAL': 'EXTERNAL';
					case 'INTERNAL', 'EXTERNAL_DATA': 'INTERNAL';
					default: 'INTERNAL';
				};
			}
		}
	}
	#end

	public static function saveSettings()
	{
		syncThemeModeFlags();
		normalizeFPSCounterPrefs();

		for (key in Reflect.fields(data))
			Reflect.setField(FlxG.save.data, key, Reflect.field(data, key));

		#if ACHIEVEMENTS_ALLOWED Achievements.save(); #end
		FlxG.save.flush();
		#if android
		StorageUtil.saveStorageTypePreference(data.storageType);
		#end

		// Wow counter =p
		Reflect.setField(FlxG.save.data, "judgementCounter", judgementCounter);
		data.judgementCounter = judgementCounter;

		// Placing this in a separate save so that it can be manually deleted without removing your Score and stuff
		var save = getControlsSave();
		save.data.keyboard = keyBinds;
		save.data.gamepad = gamepadBinds;
		save.data.mobile = mobileBinds;
		save.flush();
		FlxG.log.add("Settings saved!");
	}

	public static function loadPrefs()
	{
		#if ACHIEVEMENTS_ALLOWED Achievements.load(); #end

		for (key in Reflect.fields(data))
			if (key != 'gameplaySettings' && Reflect.hasField(FlxG.save.data, key))
				Reflect.setField(data, key, Reflect.field(FlxG.save.data, key));

		if (!Reflect.hasField(FlxG.save.data, 'fpsCounterMode'))
		{
			if (Reflect.hasField(FlxG.save.data, 'showFPS') && Reflect.field(FlxG.save.data, 'showFPS') == false)
				data.fpsCounterMode = 'Hidden';
			else if (Reflect.hasField(FlxG.save.data, 'fpsDebugLevel'))
				data.fpsCounterMode = fpsModeFromLegacy(Std.int(Reflect.field(FlxG.save.data, 'fpsDebugLevel')));
		}
		normalizeFPSCounterPrefs();

		var storedFramerateMode:Dynamic = Reflect.field(FlxG.save.data, 'framerateMode');
		if (storedFramerateMode == null)
			data.framerateMode = Reflect.hasField(FlxG.save.data,
				'fpsRework') ? ((Reflect.field(FlxG.save.data, 'fpsRework') == false) ? 'Psych' : 'Interpolated') : defaultData.framerateMode;
		else
			data.framerateMode = Std.string(storedFramerateMode);
		data.framerateMode = normalizeFramerateMode(data.framerateMode);
		syncLegacyFpsReworkFlag();
		if (!Reflect.hasField(FlxG.save.data, 'menuThemeMode'))
			data.menuThemeMode = data.menuDarkTheme ? 'Dark' : 'Light';
		syncThemeModeFlags();

		if (Main.fpsVar != null)
			Main.fpsVar.applyPrefs();

		#if (!html5 && !switch)
		FlxG.autoPause = ClientPrefs.data.autoPause;

		if (FlxG.save.data.framerate == null)
		{
			final refreshRate:Int = FlxG.stage.application.window.displayMode.refreshRate;
			data.framerate = Std.int(FlxMath.bound(refreshRate, #if mobile 30 #else 60 #end, 240));
		}
		#end

		if (Reflect.hasField(FlxG.save.data, "judgementCounter"))
			judgementCounter = !!Reflect.field(FlxG.save.data, "judgementCounter");
		judgementCounter = data.judgementCounter;

		applyFramePacing();

		#if (!html5 && !switch)
		try
		{
			if (FlxG.stage != null && FlxG.stage.application != null && FlxG.stage.application.window != null)
				Reflect.setProperty(FlxG.stage.application.window, 'vsync', data.vsync);
		}
		catch (e:Dynamic)
		{
			// Some targets may not expose window vsync.
		}
		#end

		if (FlxG.save.data.gameplaySettings != null)
		{
			var savedMap:Map<String, Dynamic> = FlxG.save.data.gameplaySettings;
			for (name => value in savedMap)
				data.gameplaySettings.set(name, value);
		}

		// flixel automatically saves your volume!
		if (FlxG.save.data.volume != null)
			FlxG.sound.volume = FlxG.save.data.volume;
		if (FlxG.save.data.mute != null)
			FlxG.sound.muted = FlxG.save.data.mute;

		#if DISCORD_ALLOWED DiscordClient.check(); #end

		// controls on a separate save file
		var save:FlxSave = new FlxSave();
		save.bind('controls_v3', CoolUtil.getSavePath());
		if (save != null)
		{
			if (save.data.keyboard != null)
			{
				var loadedControls:Map<String, Array<FlxKey>> = save.data.keyboard;
				for (control => keys in loadedControls)
					if (keyBinds.exists(control))
						keyBinds.set(control, keys);
			}
			if (save.data.gamepad != null)
			{
				var loadedControls:Map<String, Array<FlxGamepadInputID>> = save.data.gamepad;
				for (control => keys in loadedControls)
					if (gamepadBinds.exists(control))
						gamepadBinds.set(control, keys);
			}
			if (save.data.mobile != null)
			{
				var loadedControls:Map<String, Array<MobileInputID>> = save.data.mobile;
				for (control => keys in loadedControls)
					if (mobileBinds.exists(control))
						mobileBinds.set(control, keys);
			}
			reloadVolumeKeys();
		}
	}

	public static function applyFramePacing():Void
	{
		data.framerateMode = normalizeFramerateMode(data.framerateMode);
		syncLegacyFpsReworkFlag();

		var safeFramerate:Int = Std.int(Math.max(30, data.framerate));
		var drawFramerate:Int = safeFramerate;

		if (data.uncapFramerate)
		{
			FlxG.fixedTimestep = true;
			FlxG.updateFramerate = FRAMERATE_MAX;
			FlxG.drawFramerate = FRAMERATE_UNCAPPED;
			FlxG.maxElapsed = 1 / FRAMERATE_MAX;
		}
		else switch (data.framerateMode)
		{
			case 'Psych':
				FlxG.fixedTimestep = false;
				FlxG.updateFramerate = safeFramerate;
				FlxG.drawFramerate = safeFramerate;
				FlxG.maxElapsed = 0.1;

			case 'Fixed':
				FlxG.fixedTimestep = true;
				FlxG.updateFramerate = safeFramerate;
				FlxG.drawFramerate = safeFramerate;
				FlxG.maxElapsed = 1 / safeFramerate;

			default:
				drawFramerate = getInterpolatedDrawFramerate(safeFramerate);
				FlxG.fixedTimestep = true;
				FlxG.updateFramerate = safeFramerate;
				FlxG.drawFramerate = drawFramerate;
				FlxG.maxElapsed = 1 / safeFramerate;
		}

		drawFramerate = FlxG.drawFramerate;

		#if (!html5 && !switch)
		try
		{
			if (FlxG.stage != null)
			{
				FlxG.stage.frameRate = drawFramerate;
				if (FlxG.stage.window != null)
					FlxG.stage.window.frameRate = drawFramerate;
			}
		}
		catch (e:Dynamic)
		{
			// Ignore targets that do not expose window frame rate at runtime.
		}
		#end
	}

	public static function normalizeFPSCounterPrefs():Void
	{
		final modes:Array<String> = [
			'Hidden',
			'Visible No Background',
			'Visible with Background',
			'Basic Debug',
			'Extended Debug'
		];
		if (data.fpsCounterMode == null || !modes.contains(data.fpsCounterMode))
			data.fpsCounterMode = #if mobile 'Visible No Background' #else 'Visible with Background' #end;

		data.fpsDebugLevel = switch (data.fpsCounterMode)
		{
			case 'Hidden': 0;
			case 'Visible No Background': 1;
			case 'Visible with Background': 2;
			case 'Basic Debug': 3;
			case 'Extended Debug': 4;
			default: 2;
		}
		data.showFPS = data.fpsCounterMode != 'Hidden';
	}

	static function fpsModeFromLegacy(level:Int):String
	{
		return switch (level)
		{
			case 0: 'Visible No Background';
			case 1: 'Visible with Background';
			case 2: 'Basic Debug';
			case 3: 'Extended Debug';
			default: #if mobile 'Hidden' #else 'Visible with Background' #end;
		}
	}

	public static function getTargetWindowFramerate():Int
	{
		if (data.uncapFramerate)
			return FRAMERATE_UNCAPPED;

		var safeFramerate:Int = Std.int(Math.max(30, data.framerate));
		return switch (normalizeFramerateMode(data.framerateMode))
		{
			case 'Interpolated':
				getInterpolatedDrawFramerate(safeFramerate);
			default:
				safeFramerate;
		};
	}

	static function normalizeFramerateMode(mode:String):String
	{
		if (mode == null)
			return 'Psych';

		for (allowedMode in FRAMERATE_MODES)
			if (allowedMode == mode)
				return allowedMode;

		return switch (mode.toLowerCase())
		{
			case 'psych': 'Psych';
			case 'fixed': 'Fixed';
			case 'interpolated': 'Interpolated';
			default: 'Psych';
		};
	}

	static function syncLegacyFpsReworkFlag():Void
	{
		data.fpsRework = normalizeFramerateMode(data.framerateMode) != 'Psych';
	}

	public static function syncThemeModeFlags():Void
	{
		var themeMode:String = data.menuThemeMode;
		if (themeMode == null || themeMode.length == 0)
			themeMode = data.menuDarkTheme ? 'Dark' : 'Light';

		data.menuThemeMode = switch (themeMode.toLowerCase())
		{
			case 'dark': 'Dark';
			default: 'Light';
		};
		data.menuDarkTheme = data.menuThemeMode == 'Dark';
		data.menuAccentColorCustom = 0xFF000000 | (data.menuAccentColorCustom & 0x00FFFFFF);
	}

	static function getInterpolatedDrawFramerate(safeFramerate:Int):Int
	{
		#if mobile
		return safeFramerate;
		#end

		#if (!html5 && !switch)
		try
		{
			if (FlxG.stage != null && FlxG.stage.application != null && FlxG.stage.application.window != null)
			{
				var refreshRate:Int = FlxG.stage.application.window.displayMode.refreshRate;
				if (refreshRate > 0)
					return Std.int(FlxMath.bound(refreshRate, safeFramerate, Std.int(Math.min(240, safeFramerate * 2))));
			}
		}
		catch (e:Dynamic)
		{
			// Fallback to the gameplay framerate when refresh rate is unavailable.
		}
		#end

		return safeFramerate;
	}

	inline public static function getGameplaySetting(name:String, defaultValue:Dynamic = null, ?customDefaultValue:Bool = false):Dynamic
	{
		if (!customDefaultValue)
			defaultValue = defaultData.gameplaySettings.get(name);
		return /*PlayState.isStoryMode ? defaultValue : */ (data.gameplaySettings.exists(name) ? data.gameplaySettings.get(name) : defaultValue);
	}

	public static function reloadVolumeKeys()
	{
		TitleState.muteKeys = keyBinds.get('volume_mute').copy();
		TitleState.volumeDownKeys = keyBinds.get('volume_down').copy();
		TitleState.volumeUpKeys = keyBinds.get('volume_up').copy();
		toggleVolumeKeys(true);
	}

	public static function toggleVolumeKeys(?turnOn:Bool = true)
	{
		final emptyArray = [];
		FlxG.sound.muteKeys = (!Controls.instance.mobileC && turnOn) ? TitleState.muteKeys : emptyArray;
		FlxG.sound.volumeDownKeys = (!Controls.instance.mobileC && turnOn) ? TitleState.volumeDownKeys : emptyArray;
		FlxG.sound.volumeUpKeys = (!Controls.instance.mobileC && turnOn) ? TitleState.volumeUpKeys : emptyArray;
	}
}


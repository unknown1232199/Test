package backend;

import backend.AssetLoader;
import backend.ui.md3.NetworkCheckToast;
import openfl.utils.AssetType;
import openfl.utils.Assets;
import lime.utils.Assets as LimeAssets;

#if cpp
@:cppFileCode('#include <thread>')
#end
class CoolUtil
{
	public static var hasUpdate:Bool = false;
	public static var latestVersion:String = "";
	public static final haxeExtensions:Array<String> = ["hx", "hscript", "hsc", "hxs"];
	static final displaySuffixRegex:EReg = ~/\s*\([^\)]*\)\s*$/;
	static final colorWhitespaceRegex:EReg = ~/[\t\n\r]/;

	public static function checkForUpdates(url:String = null):String
	{
		if (url == null || url.length == 0)
			url = "https://raw.githubusercontent.com/Psych-Plus-Team/FNF-PlusEngine/refs/heads/main/gitVersion.txt";

		var currentVersion:String = states.MainMenuState.plusEngineVersion.trim();
		hasUpdate = false;
		latestVersion = currentVersion;

		if (ClientPrefs.data.checkForUpdates)
		{
			trace('checking for updates...');
			NetworkCheckToast.requestShow('Checking version');
			#if (target.threaded && sys)
			ThreadUtil.execAsync(function()
			{
				checkForUpdatesBlocking(url, currentVersion);
			});
			#else
			checkForUpdatesBlocking(url, currentVersion);
			#end
		}
		return currentVersion;
	}

	static function checkForUpdatesBlocking(url:String, currentVersion:String):Void
	{
		var loaded:Bool = false;
		try
		{
			var http = new haxe.Http(url);
			http.onData = function(data:String)
			{
				loaded = true;
				var remoteVersion:String = data.split('\n')[0].trim();
				trace('version online: $remoteVersion, your version: $currentVersion');

				var cmp:Int = compareVersions(currentVersion, remoteVersion);
				if (cmp == -1)
				{
					trace('update available! $currentVersion -> $remoteVersion');
					hasUpdate = true;
					latestVersion = remoteVersion;
				}
				else if (cmp == 0)
				{
					trace('versions match! no update needed');
					hasUpdate = false;
				}
				else
				{
					trace('local version is newer than remote; skipping update warning');
					hasUpdate = false;
				}

				http.onData = null;
				http.onError = null;
				http = null;
			}
			http.onError = function(error)
			{
				trace('error checking for updates: $error');
				hasUpdate = false;
			}
			http.request();
		}
		catch (e:Dynamic)
		{
			trace('error checking for updates: $e');
			hasUpdate = false;
		}
		NetworkCheckToast.requestDone(loaded ? 'Retrieved' : 'No connection');
	}

	private static function compareVersions(version1:String, version2:String):Int
	{
		if (version1 == null || version2 == null)
			return 0;

		version1 = version1.trim();
		version2 = version2.trim();
		if (version1 == version2)
			return 0;

		var v1 = parseVersion(version1);
		var v2 = parseVersion(version2);

		if (v1.major < v2.major)
			return -1;
		if (v1.major > v2.major)
			return 1;
		if (v1.minor < v2.minor)
			return -1;
		if (v1.minor > v2.minor)
			return 1;
		if (v1.patch < v2.patch)
			return -1;
		if (v1.patch > v2.patch)
			return 1;
		return 0;
	}

	private static function parseVersion(version:String):{major:Int, minor:Int, patch:Int}
	{
		var cleaned = version.split('-')[0];
		cleaned = cleaned.split('+')[0];
		cleaned = normalizeDisplaySuffix(cleaned);
		var parts = cleaned.split('.');

		inline function toIntOr0(v:String):Int
		{
			var parsed:Null<Int> = Std.parseInt(v);
			return parsed == null ? 0 : parsed;
		}

		var major:Int = parts.length > 0 ? toIntOr0(parts[0]) : 0;
		var minor:Int = parts.length > 1 ? toIntOr0(parts[1]) : 0;
		var patch:Int = parts.length > 2 ? toIntOr0(parts[2]) : 0;
		return {major: major, minor: minor, patch: patch};
	}

	private static function normalizeDisplaySuffix(version:String):String
	{
		if (version == null)
			return "";
		var trimmed = version.trim();
		if (displaySuffixRegex.match(trimmed))
			return displaySuffixRegex.matchedLeft().trim();
		return trimmed;
	}

	inline public static function quantize(f:Float, snap:Float)
	{
		// changed so this actually works lol
		var m:Float = Math.fround(f * snap);
		// trace(snap);
		return (m / snap);
	}

	inline public static function capitalize(text:String)
		return text.charAt(0).toUpperCase() + text.substr(1).toLowerCase();

	public static function boundTo(value:Float, min:Float, max:Float):Float
	{
		var newValue:Float = value;
		if (newValue < min)
			newValue = min;
		else if (newValue > max)
			newValue = max;
		return newValue;
	}

	inline public static function coolTextFile(path:String):Array<String>
	{
		var daList:String = AssetLoader.loadText(path);
		return daList != null ? listFromString(daList) : [];
	}

	inline public static function colorFromString(color:String):FlxColor
	{
		var color:String = colorWhitespaceRegex.split(color).join('').trim();
		if (color.startsWith('0x'))
			color = color.substring(color.length - 6);

		var colorNum:Null<FlxColor> = FlxColor.fromString(color);
		if (colorNum == null)
			colorNum = FlxColor.fromString('#$color');
		return colorNum != null ? colorNum : FlxColor.WHITE;
	}

	inline public static function listFromString(string:String):Array<String>
	{
		var daList:Array<String> = [];
		daList = string.trim().split('\n');

		for (i in 0...daList.length)
			daList[i] = daList[i].trim();

		return daList;
	}

	public static function floorDecimal(value:Float, decimals:Int):Float
	{
		if (decimals < 1)
			return Math.floor(value);

		return Math.floor(value * Math.pow(10, decimals)) / Math.pow(10, decimals);
	}

	#if linux
	public static function sortAlphabetically(list:Array<String>):Array<String>
	{
		if (list == null)
			return [];

		list.sort((a, b) ->
		{
			var upperA = a.toUpperCase();
			var upperB = b.toUpperCase();

			return upperA < upperB ? -1 : upperA > upperB ? 1 : 0;
		});
		return list;
	}
	#end

	inline public static function dominantColor(sprite:flixel.FlxSprite):Int
	{
		var countByColor:Map<Int, Int> = [];
		for (col in 0...sprite.frameWidth)
		{
			for (row in 0...sprite.frameHeight)
			{
				var colorOfThisPixel:FlxColor = sprite.pixels.getPixel32(col, row);
				if (colorOfThisPixel.alphaFloat > 0.05)
				{
					colorOfThisPixel = FlxColor.fromRGB(colorOfThisPixel.red, colorOfThisPixel.green, colorOfThisPixel.blue, 255);
					var count:Int = countByColor.exists(colorOfThisPixel) ? countByColor[colorOfThisPixel] : 0;
					countByColor[colorOfThisPixel] = count + 1;
				}
			}
		}

		var maxCount = 0;
		var maxKey:Int = 0; // after the loop this will store the max color
		countByColor[FlxColor.BLACK] = 0;
		for (key => count in countByColor)
		{
			if (count >= maxCount)
			{
				maxCount = count;
				maxKey = key;
			}
		}
		countByColor = [];
		return maxKey;
	}

	inline public static function numberArray(max:Int, ?min = 0):Array<Int>
	{
		var dumbArray:Array<Int> = [];
		for (i in min...max)
			dumbArray.push(i);

		return dumbArray;
	}

	inline public static function browserLoad(site:String)
	{
		#if linux
		Sys.command('/usr/bin/xdg-open', [site]);
		#else
		FlxG.openURL(site);
		#end
	}

	inline public static function openFolder(folder:String, absolute:Bool = false)
	{
		#if sys
		if (!absolute)
			folder = Sys.getCwd() + '$folder';

		folder = folder.replace('/', '\\');
		if (folder.endsWith('/'))
			folder.substr(0, folder.length - 1);

		#if linux
		var command:String = '/usr/bin/xdg-open';
		#else
		var command:String = 'explorer.exe';
		#end
		Sys.command(command, [folder]);
		trace('$command $folder');
		#else
		FlxG.error("Platform is not supported for CoolUtil.openFolder");
		#end
	}

	/**
		Helper Function to Fix Save Files for Flixel 5

		-- EDIT: [November 29, 2023] --

		this function is used to get the save path, period.
		since newer flixel versions are being enforced anyways.
		@crowplexus
	**/
	@:access(flixel.util.FlxSave.validate)
	inline public static function getSavePath():String
	{
		final company:String = FlxG.stage.application.meta.get('company');
		// #if (flixel < "5.0.0") return company; #else
		return '${company}/${flixel.util.FlxSave.validate(FlxG.stage.application.meta.get('file'))}';
		// #end
	}

	public static function setTextBorderFromString(text:FlxText, border:String)
	{
		switch (border.toLowerCase().trim())
		{
			case 'shadow':
				text.borderStyle = SHADOW;
			case 'outline':
				text.borderStyle = OUTLINE;
			case 'outline_fast', 'outlinefast':
				text.borderStyle = OUTLINE_FAST;
			default:
				text.borderStyle = NONE;
		}
	}

	public static function showPopUp(message:String, title:String):Void
	{
		#if android
		if (mobile.backend.AndroidNative.showAlert(title, message))
			return;
		#end

		if (FlxG.stage != null && FlxG.stage.window != null)
			FlxG.stage.window.alert(message, title);
	}

	public static function showToast(message:String, ?long:Bool = false):Void
	{
		if (message == null || message.length == 0)
			return;

		#if android
		if (mobile.backend.AndroidNative.showToast(message, long))
			return;
		#end
	}

	#if cpp
	@:functionCode('
        return std::thread::hardware_concurrency();
    ')
	#end
	public static function getCPUThreadsCount():Int
	{
		return 1;
	}
}


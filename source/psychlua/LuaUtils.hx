package psychlua;

import backend.WeekData;
import objects.Character;
import objects.Note;
import backend.StageData;

import flixel.FlxBasic;
import openfl.display.BlendMode;
import Type.ValueType;

import substates.GameOverSubstate;

typedef LuaTweenOptions = {
	type:FlxTweenType,
	startDelay:Float,
	onUpdate:Null<String>,
	onStart:Null<String>,
	onComplete:Null<String>,
	loopDelay:Float,
	ease:EaseFunction
}

class LuaUtils
{
	public static final Function_Stop:String = "##PSYCHLUA_FUNCTIONSTOP";
	public static final Function_Continue:String = "##PSYCHLUA_FUNCTIONCONTINUE";
	public static final Function_StopLua:String = "##PSYCHLUA_FUNCTIONSTOPLUA";
	public static final Function_StopHScript:String = "##PSYCHLUA_FUNCTIONSTOPHSCRIPT";
	public static final Function_StopAll:String = "##PSYCHLUA_FUNCTIONSTOPALL";

	public static function isStop(ret:Dynamic):Bool
	{
		return ret == Function_Stop
			|| ret == Function_StopLua
			|| ret == Function_StopHScript
			|| ret == Function_StopAll
			|| ret == 1;
	}

	public static function getCurrentContext():Null<LuaHostContext>
	{
		#if LUA_ALLOWED
		if (FunkinLua.lastCalledScript != null)
			return FunkinLua.lastCalledScript.context;
		#end
		return null;
	}

	public static function getLuaTween(options:Dynamic)
	{
		return (options != null) ? {
			type: getTweenTypeByString(options.type),
			startDelay: options.startDelay,
			onUpdate: options.onUpdate,
			onStart: options.onStart,
			onComplete: options.onComplete,
			loopDelay: options.loopDelay,
			ease: getTweenEaseByString(options.ease)
		} : null;
	}

	public static function setVarInArray(instance:Dynamic, variable:String, value:Dynamic, allowMaps:Bool = false):Any
	{
		var splitProps:Array<String> = variable.split('[');
		if(splitProps.length > 1)
		{
			var target:Dynamic = null;
			if(MusicBeatState.getVariables().exists(splitProps[0]))
			{
				var retVal:Dynamic = MusicBeatState.getVariables().get(splitProps[0]);
				if(retVal != null)
					target = retVal;
			}
			else target = Reflect.getProperty(instance, splitProps[0]);

			for (i in 1...splitProps.length)
			{
				var j:Dynamic = splitProps[i].substr(0, splitProps[i].length - 1);
				if(i >= splitProps.length-1) //Last array
					target[j] = value;
				else //Anything else
					target = target[j];
			}
			return target;
		}

		if(allowMaps && isMap(instance))
		{
			//trace(instance);
			instance.set(variable, value);
			return value;
		}

		if(setLegacyNoteSplashProperty(instance, variable, value))
			return value;

		if(instance is MusicBeatState && MusicBeatState.getVariables().exists(variable))
		{
			MusicBeatState.getVariables().set(variable, value);
			return value;
		}
		Reflect.setProperty(instance, variable, value);
		return value;
	}
	public static function getVarInArray(instance:Dynamic, variable:String, allowMaps:Bool = false):Any
	{
		var splitProps:Array<String> = variable.split('[');
		if(splitProps.length > 1)
		{
			var target:Dynamic = null;
			if(MusicBeatState.getVariables().exists(splitProps[0]))
			{
				var retVal:Dynamic = MusicBeatState.getVariables().get(splitProps[0]);
				if(retVal != null)
					target = retVal;
			}
			else
				target = Reflect.getProperty(instance, splitProps[0]);

			for (i in 1...splitProps.length)
			{
				var j:Dynamic = splitProps[i].substr(0, splitProps[i].length - 1);
				target = target[j];
			}
			return target;
		}
		
		if(allowMaps && isMap(instance))
		{
			//trace(instance);
			return instance.get(variable);
		}

		if(hasLegacyNoteSplashProperty(instance, variable))
			return getLegacyNoteSplashProperty(instance, variable);

		if(instance is MusicBeatState && MusicBeatState.getVariables().exists(variable))
		{
			var retVal:Dynamic = MusicBeatState.getVariables().get(variable);
			if(retVal != null)
				return retVal;
		}
		return Reflect.getProperty(instance, variable);
	}

	public static function getModSetting(saveTag:String, ?modName:String = null)
	{
		#if MODS_ALLOWED
		if(FlxG.save.data.modSettings == null) FlxG.save.data.modSettings = new Map<String, Dynamic>();

		var settings:Map<String, Dynamic> = FlxG.save.data.modSettings.get(modName);
		var path:String = Paths.mods('$modName/data/settings.json');
		if(FileSystem.exists(path))
		{
			if(settings == null || !settings.exists(saveTag))
			{
				if(settings == null) settings = new Map<String, Dynamic>();
				var data:String = File.getContent(path);
				try
				{
					//FunkinLua.luaTrace('getModSetting: Trying to find default value for "$saveTag" in Mod: "$modName"');
					var parsedJson:Dynamic = tjson.TJSON.parse(data);
					for (i in 0...parsedJson.length)
					{
						var sub:Dynamic = parsedJson[i];
						if(sub != null && sub.save != null && !settings.exists(sub.save))
						{
							if(sub.type != 'keybind' && sub.type != 'key')
							{
								if(sub.value != null)
								{
									//FunkinLua.luaTrace('getModSetting: Found unsaved value "${sub.save}" in Mod: "$modName"');
									settings.set(sub.save, sub.value);
								}
							}
							else
							{
								//FunkinLua.luaTrace('getModSetting: Found unsaved keybind "${sub.save}" in Mod: "$modName"');
								settings.set(sub.save, {keyboard: (sub.keyboard != null ? sub.keyboard : 'NONE'), gamepad: (sub.gamepad != null ? sub.gamepad : 'NONE')});
							}
						}
					}
					FlxG.save.data.modSettings.set(modName, settings);
				}
				catch(e:Dynamic)
				{
					var errorTitle = 'Mod name: ' + Mods.currentModDirectory;
					var errorMsg = 'An error occurred: $e';
					CoolUtil.showPopUp(errorMsg, errorTitle);
				}
			}
		}
		else
		{
			FlxG.save.data.modSettings.remove(modName);
			#if LUA_ALLOWED
			FunkinLua.luaTrace('getModSetting: $path could not be found!', true, false, FlxColor.RED);
			#elseif HSCRIPT_ALLOWED
			if (PlayState.instance != null) PlayState.instance.addTextToDebug('getModSetting: $path could not be found!', FlxColor.RED);
			else FlxG.log.warn('getModSetting: $path could not be found!');
			#else
			FlxG.log.warn('getModSetting: $path could not be found!');
			#end
			return null;
		}

		if(settings.exists(saveTag)) return settings.get(saveTag);
		#if LUA_ALLOWED
		FunkinLua.luaTrace('getModSetting: "$saveTag" could not be found inside $modName\'s settings!', true, false, FlxColor.RED);
		#elseif HSCRIPT_ALLOWED
		if (PlayState.instance != null) PlayState.instance.addTextToDebug('getModSetting: "$saveTag" could not be found inside $modName\'s settings!', FlxColor.RED);
		else FlxG.log.warn('getModSetting: "$saveTag" could not be found inside $modName\'s settings!');
		#else
		FlxG.log.warn('getModSetting: "$saveTag" could not be found inside $modName\'s settings!');
		#end
		#end
		return null;
	}
	
	public static function isMap(variable:Dynamic)
	{
		/*switch(Type.typeof(variable)){
			case ValueType.TClass(haxe.ds.StringMap) | ValueType.TClass(haxe.ds.ObjectMap) | ValueType.TClass(haxe.ds.IntMap) | ValueType.TClass(haxe.ds.EnumValueMap):
				return true;
			default:
				return false;
		}*/

		//trace(variable);
		if(variable.exists != null && variable.keyValueIterator != null) return true;
		return false;
	}

	public static function setGroupStuff(leArray:Dynamic, variable:String, value:Dynamic, ?allowMaps:Bool = false) {
		var split:Array<String> = variable.split('.');
		if(split.length > 1) {
			var obj:Dynamic = Reflect.getProperty(leArray, split[0]);
			for (i in 1...split.length-1)
				obj = Reflect.getProperty(obj, split[i]);

			leArray = obj;
			variable = split[split.length-1];
		}
		if(setLegacyNoteSplashProperty(leArray, variable, value))
			return value;
		if(allowMaps && isMap(leArray)) leArray.set(variable, value);
		else Reflect.setProperty(leArray, variable, value);
		return value;
	}
	public static function getGroupStuff(leArray:Dynamic, variable:String, ?allowMaps:Bool = false) {
		var split:Array<String> = variable.split('.');
		if(split.length > 1) {
			var obj:Dynamic = Reflect.getProperty(leArray, split[0]);
			for (i in 1...split.length-1)
				obj = Reflect.getProperty(obj, split[i]);

			leArray = obj;
			variable = split[split.length-1];
		}

		if(hasLegacyNoteSplashProperty(leArray, variable))
			return getLegacyNoteSplashProperty(leArray, variable);

		if(allowMaps && isMap(leArray)) return leArray.get(variable);
		return Reflect.getProperty(leArray, variable);
	}

	static function hasLegacyNoteSplashProperty(instance:Dynamic, variable:String):Bool
	{
		return instance != null && Std.isOfType(instance, Note) && (variable == 'noteSplashDisabled' || variable == 'noteSplashTexture');
	}

	static function setLegacyNoteSplashProperty(instance:Dynamic, variable:String, value:Dynamic):Bool
	{
		if(!hasLegacyNoteSplashProperty(instance, variable))
			return false;

		var note:Note = cast instance;
		switch(variable)
		{
			case 'noteSplashDisabled':
				StructurePsychOld.warnLegacyLuaUsage('noteSplashDisabled', 'noteSplashData.disabled');
				note.noteSplashData.disabled = value == true;
			case 'noteSplashTexture':
				StructurePsychOld.warnLegacyLuaUsage('noteSplashTexture', 'noteSplashData.texture');
				note.noteSplashData.texture = value == null ? null : Std.string(value);
		}
		return true;
	}

	static function getLegacyNoteSplashProperty(instance:Dynamic, variable:String):Dynamic
	{
		var note:Note = cast instance;
		switch(variable)
		{
			case 'noteSplashDisabled':
				StructurePsychOld.warnLegacyLuaUsage('noteSplashDisabled', 'noteSplashData.disabled');
			case 'noteSplashTexture':
				StructurePsychOld.warnLegacyLuaUsage('noteSplashTexture', 'noteSplashData.texture');
		}
		return switch(variable)
		{
			case 'noteSplashDisabled': note.noteSplashData.disabled;
			case 'noteSplashTexture': note.noteSplashData.texture;
			default: null;
		}
	}

	public static function getPropertyLoop(split:Array<String>, ?getProperty:Bool=true, ?allowMaps:Bool = false):Dynamic
	{
		var obj:Dynamic = getObjectDirectly(split[0]);
		var end = split.length;
		if(getProperty) end = split.length-1;

		for (i in 1...end) obj = getVarInArray(obj, split[i], allowMaps);
		return obj;
	}

	public static function getObjectDirectly(objectName:String, ?allowMaps:Bool = false):Dynamic
	{
		switch(objectName)
		{
			case 'this' | 'instance' | 'game':
				return getTargetInstance();
			
			default:
				var obj:Dynamic = MusicBeatState.getVariables().get(objectName);
				if(obj == null)
				{
					var ctx = getCurrentContext();
					if(ctx != null && ctx.variables != null && ctx.variables.exists(objectName))
						obj = ctx.variables.get(objectName);
				}
				if(obj == null) obj = getVarInArray(getTargetInstance(), objectName, allowMaps);
				return obj;
		}
	}
	
	public static function isOfTypes(value:Any, types:Array<Dynamic>)
	{
		for (type in types)
		{
			if(Std.isOfType(value, type)) return true;
		}
		return false;
	}
	public static function isLuaSupported(value:Any):Bool {
		return (value == null || isOfTypes(value, [Bool, Int, Float, String, Array]) || Type.typeof(value) == ValueType.TObject);
	}
	
	public static function getTargetInstance()
	{
		var ctx = getCurrentContext();
		if(ctx != null && ctx.host != null) return ctx.host;
		if(PlayState.instance != null) return PlayState.instance.isDead ? GameOverSubstate.instance : PlayState.instance;
		return MusicBeatState.getState();
	}

	public static inline function getLowestCharacterGroup():FlxSpriteGroup
	{
		var stageData:StageFile = StageData.getStageFile(PlayState.SONG.stage);
		var group:FlxSpriteGroup = (stageData.hide_girlfriend ? PlayState.instance.boyfriendGroup : PlayState.instance.gfGroup);

		var pos:Int = PlayState.instance.members.indexOf(group);

		var newPos:Int = PlayState.instance.members.indexOf(PlayState.instance.boyfriendGroup);
		if(newPos < pos)
		{
			group = PlayState.instance.boyfriendGroup;
			pos = newPos;
		}
		
		newPos = PlayState.instance.members.indexOf(PlayState.instance.dadGroup);
		if(newPos < pos)
		{
			group = PlayState.instance.dadGroup;
			pos = newPos;
		}
		return group;
	}
	
	public static function addAnimByIndices(obj:String, name:String, prefix:String, indices:Any = null, framerate:Float = 24, loop:Bool = false)
	{
		var obj:FlxSprite = cast LuaUtils.getObjectDirectly(obj);
		if(obj != null && obj.animation != null)
		{
			if(indices == null)
				indices = [0];
			else if(Std.isOfType(indices, String))
			{
				var strIndices:Array<String> = cast (indices, String).trim().split(',');
				var myIndices:Array<Int> = [];
				for (i in 0...strIndices.length) {
					myIndices.push(Std.parseInt(strIndices[i]));
				}
				indices = myIndices;
			}

			if(prefix != null) obj.animation.addByIndices(name, prefix, indices, '', framerate, loop);
			else obj.animation.add(name, indices, framerate, loop);

			if(obj.animation.curAnim == null)
			{
				var dyn:Dynamic = cast obj;
				if(dyn.playAnim != null) dyn.playAnim(name, true);
				else dyn.animation.play(name, true);
			}
			return true;
		}
		return false;
	}
	
	public static function loadFrames(spr:FlxSprite, image:String, spriteType:String)
	{
		switch(spriteType.toLowerCase().replace(' ', ''))
		{
			//case "texture" | "textureatlas" | "tex":
				//spr.frames = AtlasFrameMaker.construct(image);

			//case "texture_noaa" | "textureatlas_noaa" | "tex_noaa":
				//spr.frames = AtlasFrameMaker.construct(image, null, true);

			case 'aseprite', 'ase', 'json', 'jsoni8':
				spr.frames = Paths.getAsepriteAtlas(image);

			case "packer", 'packeratlas', 'pac':
				spr.frames = Paths.getPackerAtlas(image);

			case 'sparrow', 'sparrowatlas', 'sparrowv2':
				spr.frames = Paths.getSparrowAtlas(image);

			default:
				spr.frames = Paths.getAtlas(image);
		}
	}

	public static function destroyObject(tag:String) {
		var variables = MusicBeatState.getVariables();
		var obj:Dynamic = variables.get(tag);
		if(obj == null || !Std.isOfType(obj, FlxBasic) || obj.destroy == null)
			return;

		LuaUtils.getTargetInstance().remove(obj, true);
		obj.destroy();
		variables.remove(tag);
	}

	static inline function normalizeTweenTag(tag:String):String {
		var formatted:String = formatVariable(tag);
		return formatted.startsWith('tween_') ? formatted.substr('tween_'.length) : formatted;
	}

	public static function storeTween(tag:String, tween:FlxTween):String {
		if(tag == null || tween == null) return null;

		var rawTag:String = normalizeTweenTag(tag);
		var prefixedTag:String = 'tween_' + rawTag;
		#if LUA_ALLOWED
		if(PlayState.instance != null)
			PlayState.instance.modchartTweens.set(rawTag, tween);
		#end
		MusicBeatState.getVariables().set(prefixedTag, tween);
		return rawTag;
	}

	public static function removeTween(tag:String):Void {
		if(tag == null) return;

		var rawTag:String = normalizeTweenTag(tag);
		var prefixedTag:String = 'tween_' + rawTag;
		#if LUA_ALLOWED
		if(PlayState.instance != null)
		{
			PlayState.instance.modchartTweens.remove(rawTag);
			PlayState.instance.modchartTweens.remove(prefixedTag);
		}
		#end
		var variables = MusicBeatState.getVariables();
		var rawValue:Dynamic = variables.get(rawTag);
		if(Std.isOfType(rawValue, FlxTween))
			variables.remove(rawTag);
		variables.remove(prefixedTag);
	}

	public static function cancelTween(tag:String) {
		if(tag == null) return;

		var rawTag:String = normalizeTweenTag(tag);
		var prefixedTag:String = 'tween_' + rawTag;
		var found:Array<FlxTween> = [];
		#if LUA_ALLOWED
		if(PlayState.instance != null)
		{
			var legacyTween:FlxTween = PlayState.instance.modchartTweens.get(rawTag);
			if(legacyTween != null) found.push(legacyTween);

			var prefixedTween:FlxTween = PlayState.instance.modchartTweens.get(prefixedTag);
			if(prefixedTween != null && found.indexOf(prefixedTween) < 0) found.push(prefixedTween);
		}
		#end

		var variables = MusicBeatState.getVariables();
		var rawValue:Dynamic = variables.get(rawTag);
		if(Std.isOfType(rawValue, FlxTween) && found.indexOf(rawValue) < 0)
			found.push(rawValue);

		var tweenValue:Dynamic = variables.get(prefixedTag);
		if(Std.isOfType(tweenValue, FlxTween) && found.indexOf(tweenValue) < 0)
			found.push(tweenValue);

		for(twn in found)
		{
			twn.cancel();
			twn.destroy();
		}
		removeTween(rawTag);
	}

	public static function cancelTimer(tag:String) {
		if(!tag.startsWith('timer_')) tag = 'timer_' + LuaUtils.formatVariable(tag);
		var variables = MusicBeatState.getVariables();
		var tmr:FlxTimer = variables.get(tag);
		if(tmr != null)
		{
			tmr.cancel();
			tmr.destroy();
			variables.remove(tag);
		}
	}

	public static function formatVariable(tag:String)
		return tag.trim().replace(' ', '_').replace('.', '');

	public static function tweenPrepare(tag:String, vars:String) {
		if(tag != null) cancelTween(tag);
		var variables:Array<String> = vars.split('.');
		var sexyProp:Dynamic = LuaUtils.getObjectDirectly(variables[0]);
		if(variables.length > 1) sexyProp = LuaUtils.getVarInArray(LuaUtils.getPropertyLoop(variables), variables[variables.length-1]);
		return sexyProp;
	}

	public static function getBuildTarget():String
	{
		#if windows
		#if x86_BUILD
		return 'windows_x86';
		#else
		return 'windows';
		#end
		#elseif linux
		return 'linux';
		#elseif mac
		return 'mac';
		#elseif hl
		return 'hashlink';
		#elseif (html5 || emscripten || nodejs || winjs || electron)
		return 'browser';
		#elseif android
		return 'android';
		#elseif webos
		return 'webos';
		#elseif tvos
		return 'tvos';
		#elseif watchos
		return 'watchos';
		#elseif air
		return 'air';
		#elseif flash
		return 'flash';
		#elseif (ios || iphone || iphonesim)
		return 'ios';
		#elseif neko
		return 'neko';
		#elseif switch
		return 'switch';
		#else
		return 'unknown';
		#end
	}

	//buncho string stuffs
	public static function getTweenTypeByString(?type:String = '') {
		switch(type.toLowerCase().trim())
		{
			case 'backward': return FlxTweenType.BACKWARD;
			case 'looping'|'loop': return FlxTweenType.LOOPING;
			case 'persist': return FlxTweenType.PERSIST;
			case 'pingpong': return FlxTweenType.PINGPONG;
		}
		return FlxTweenType.ONESHOT;
	}

	public static function getTweenEaseByString(?ease:String = '') {
		switch(ease.toLowerCase().trim()) {
			case 'accelerate': return FlxEase.quadIn;
			case 'backin': return FlxEase.backIn;
			case 'backinout': return FlxEase.backInOut;
			case 'backout': return FlxEase.backOut;
			case 'backoutin': return FlxEase.backInOut;
			case 'bell': return FlxEase.sineInOut;
			case 'bounce': return FlxEase.bounceOut;
			case 'bouncein': return FlxEase.bounceIn;
			case 'bounceinout': return FlxEase.bounceInOut;
			case 'bounceout': return FlxEase.bounceOut;
			case 'bounceoutin': return FlxEase.bounceInOut;
			case 'circin': return FlxEase.circIn;
			case 'circinout': return FlxEase.circInOut;
			case 'circout': return FlxEase.circOut;
			case 'circoutin': return FlxEase.circInOut;
			case 'cubein': return FlxEase.cubeIn;
			case 'cubeinout': return FlxEase.cubeInOut;
			case 'cubeout': return FlxEase.cubeOut;
			case 'cubicoutin': return FlxEase.cubeInOut;
			case 'decelerate': return FlxEase.quadOut;
			case 'elasticin': return FlxEase.elasticIn;
			case 'elasticinout': return FlxEase.elasticInOut;
			case 'elasticout': return FlxEase.elasticOut;
			case 'elasticoutin': return FlxEase.elasticInOut;
			case 'emphasizedaccelerate': return FlxEase.quartIn;
			case 'emphasizeddecelerate': return FlxEase.quartOut;
			case 'expoin': return FlxEase.expoIn;
			case 'expoinout': return FlxEase.expoInOut;
			case 'expoout': return FlxEase.expoOut;
			case 'expooutin': return FlxEase.expoInOut;
			case 'instant': return FlxEase.linear;
			case 'inverse': return FlxEase.quadOut;
			case 'pop': return FlxEase.backOut;
			case 'tap': return FlxEase.quadInOut;
			case 'pulse': return FlxEase.sineInOut;
			case 'spike': return FlxEase.quadInOut;
			case 'standard': return FlxEase.quadInOut;
			case 'tri': return FlxEase.sineInOut;
			case 'quadin': return FlxEase.quadIn;
			case 'quadinout': return FlxEase.quadInOut;
			case 'quadout': return FlxEase.quadOut;
			case 'quadoutin': return FlxEase.quadInOut;
			case 'quartin': return FlxEase.quartIn;
			case 'quartinout': return FlxEase.quartInOut;
			case 'quartout': return FlxEase.quartOut;
			case 'quartoutin': return FlxEase.quartInOut;
			case 'quintin': return FlxEase.quintIn;
			case 'quintinout': return FlxEase.quintInOut;
			case 'quintout': return FlxEase.quintOut;
			case 'quintoutin': return FlxEase.quintInOut;
			case 'sinein': return FlxEase.sineIn;
			case 'sineinout': return FlxEase.sineInOut;
			case 'sineout': return FlxEase.sineOut;
			case 'sineoutin': return FlxEase.sineInOut;
			case 'smoothstepin': return FlxEase.smoothStepIn;
			case 'smoothstepinout': return FlxEase.smoothStepInOut;
			case 'smoothstepout': return FlxEase.smoothStepOut;
			case 'smootherstepin': return FlxEase.smootherStepIn;
			case 'smootherstepinout': return FlxEase.smootherStepInOut;
			case 'smootherstepout': return FlxEase.smootherStepOut;
		}
		return FlxEase.linear;
	}

	public static function blendModeFromString(blend:String):BlendMode {
		switch(blend.toLowerCase().trim()) {
			case 'add': return ADD;
			case 'alpha': return ALPHA;
			case 'darken': return DARKEN;
			case 'difference': return DIFFERENCE;
			case 'erase': return ERASE;
			case 'hardlight': return HARDLIGHT;
			case 'invert': return INVERT;
			case 'layer': return LAYER;
			case 'lighten': return LIGHTEN;
			case 'multiply': return MULTIPLY;
			case 'overlay': return OVERLAY;
			case 'screen': return SCREEN;
			case 'shader': return SHADER;
			case 'subtract': return SUBTRACT;
		}
		return NORMAL;
	}
	
	public static function typeToString(type:Int):String {
		#if LUA_ALLOWED
		switch(type) {
			case Lua.LUA_TBOOLEAN: return "boolean";
			case Lua.LUA_TNUMBER: return "number";
			case Lua.LUA_TSTRING: return "string";
			case Lua.LUA_TTABLE: return "table";
			case Lua.LUA_TFUNCTION: return "function";
		}
		if (type <= Lua.LUA_TNIL) return "nil";
		#end
		return "unknown";
	}

	public static function cameraFromString(cam:String):FlxCamera {
		var lowerCam:String = cam == null ? '' : cam.toLowerCase();
		if(PlayState.instance != null) {
			switch(lowerCam) {
				case 'camgame' | 'game': return PlayState.instance.camGame;
				case 'camhud' | 'hud': return PlayState.instance.camHUD;
				case 'camother' | 'other': return PlayState.instance.camOther;
			}
		}
		var camera:FlxCamera = MusicBeatState.getVariables().get(cam);
		if (camera == null || !Std.isOfType(camera, FlxCamera))
		{
			var host = getTargetInstance();
			var fieldName:String = switch(lowerCam) {
				case 'camgame' | 'game': 'camGame';
				case 'camhud' | 'hud': 'camHUD';
				case 'camother' | 'other': 'camOther';
				default: cam;
			}
			var reflected:Dynamic = (host != null && fieldName != null && fieldName.length > 0) ? Reflect.getProperty(host, fieldName) : null;
			if(reflected != null && Std.isOfType(reflected, FlxCamera)) camera = reflected;
		}
		if (camera == null || !Std.isOfType(camera, FlxCamera)) camera = FlxG.camera;
		return camera;
	}
}
